! Lexc grammar for generating diminutives from bare nouns and nouns
! with an augmentative suffix in Portuguese
! Author: Leonel F. de Alencar, Federal University of Cear√°
! Date: April 11, 2018

LEXICON Root
	Nouns ;

LEXICON Nouns

< @stxt"all_other_m_sg" "@U.GEND.M@" "@U.NUM.SG@" >	Cat ;
< @stxt"all_other_m_pl" "@U.GEND.M@" "@U.NUM.PL@" >	Cat ;
< @stxt"all_other_f_sg" "@U.GEND.F@" "@U.NUM.SG@" >	Cat ;
< @stxt"all_other_f_pl" "@U.GEND.F@" "@U.NUM.PL@" >	Cat ;
< @stxt"stems_in_s_m_sg"  "@U.GEND.M@" "@U.NUM.SG@" "@U.ENDING.S@" > Cat ;
< @stxt"stems_in_s_m_pl"  "@U.GEND.M@" "@U.NUM.PL@" "@U.ENDING.S@" > Cat ;
< @stxt"stems_in_s_f_sg"  "@U.GEND.F@" "@U.NUM.SG@" "@U.ENDING.S@" > Cat ;
< @stxt"stems_in_s_f_pl"  "@U.GEND.F@" "@U.NUM.PL@" "@U.ENDING.S@" > Cat ;
< @stxt"aug_m_sg" "@U.GEND.M@" "@U.NUM.SG@" "@U.AUG.Y@" >	Cat ;
< @stxt"aug_m_pl" "@U.GEND.M@" "@U.NUM.PL@" "@U.AUG.Y@" >	Cat ;
< @stxt"aug_f_sg" "@U.GEND.F@" "@U.NUM.SG@" "@U.AUG.Y@" >	Cat ;
< @stxt"aug_f_pl" "@U.GEND.F@" "@U.NUM.PL@" "@U.AUG.Y@" >	Cat ;

LEXICON Cat
< %+N:0 "@D.AUG@" >	Suff ;
< %+N:0 %+AUG:0 "@R.AUG@" >	Suff ;

LEXICON Suff
<%+DIM 0:%^ "@D.ENDING.S@" "@U.NUM.SG@" "@C.NUM@" >	Gend ;
<%+DIM:{^zinh} 0:%^  "@D.ENDING.S@" >	Gend ;
<%+DIM:{^inh} 0:%^ "@R.ENDING.S@" >	Gend ;

LEXICON Gend 
< %+F:a 0:%^ "@U.GEND.F@" >	Num ;
< %+M:o 0:%^ "@U.GEND.M@" >	Num ;

LEXICON Num
< %+SG:0 "@U.NUM.SG@" >	# ;
< %+PL:s	"@U.NUM.PL@" > # ;
